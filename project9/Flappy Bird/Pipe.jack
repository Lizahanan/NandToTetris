class Pipe
{
  /*
  Models an individual Pipe object and creates/updates the obstacle image.
  */
  field int width;                  // Image full-visibility width.
  field int width_current;          // Current width (for entering/leaving display).
  field int width_increment;        // Amount to shift obstacle for each step.
  field int x_coord;               // Current x-coordinate for obstacle image.
  field int gap_size;               // Distance between top and bottom pieces.
  field int gap_top;                // Coordinate for bottom of top piece.
  field int gap_bottom;             // Coordinate for top of bottom piece.
  field int screen_width;           // Screen width.
  field int screen_height;          // Screen height.

  /*
  Construct new Pipe object.

  @param int gap_offset Offset of obstacle's gap.
  */
  constructor Pipe new(int gap_offset) {
    let width = 12;
    let width_increment = 1;
    let width_current = width_increment;
    let x_coord = 509;
    let screen_width = 512;
    let screen_height = 256;
    let gap_size = 65;
    let gap_top = computeGapTop(gap_offset);
    let gap_bottom = (gap_top + gap_size);

    return this;
  }

  /*
  Calculate coordinate for bottom of top piece.
  */
  method int computeGapTop(int offset)
  {
    var int remainder;
    let remainder = Mod.remainder(offset, 6);

    return (remainder + 1) * 30;
  }

  /*
  Shift complete obstacle image to the left.
  */
  method void shift()
  {
    if (x_coord < (screen_width - width )) {
      do Screen.setColor(false);
      do Screen.drawRectangle(x_coord + width_current - width_increment, 0, x_coord + width_current, gap_top);
      do Screen.drawRectangle(x_coord + width_current - width_increment, gap_bottom, x_coord + width_current, 255);
    }

    return;
  }

  /*
  Move obstacle image to the left. Deleting image entirely if coords no longer
  on display.
  */
  method void moveLeft()
  {
    do shift();

    // Width_current is either growing or shrinking
    if (width_current < width) {
      if (x_coord > 0) {
        let width_current = width_current + width_increment;
      }
    }

    if (x_coord > 1) {
      let x_coord = x_coord - width_increment;
    } else {
      let x_coord = 0;
      let width_current = width_current - width_increment;
    }

    return;
  }

  /*
  Get current width.
  */
  method int getWidthCurrent()
  {
    return width_current;
  }

  /*
  Get width shift increment.
  */
  method int getWidthIncrement()
  {
    return width_increment;
  }

  /*
  Get current x-coordinate.
  */
  method int getXOffset()
  {
    return x_coord;
  }

  /*
  Get y-coordinate of top of gap.
  */
  method int getGapTop()
  {
    return gap_top;
  }

  /*
  Get y-coordinate of bottom of gap.
  */
  method int getGapBottom()
  {
    return gap_bottom;
  }

  /*
  Draw obstacle at current x-offset.
  */
  method void render()
  {
    if (x_coord - width_increment > 0) {
      do Screen.setColor(true);
      do Screen.drawRectangle(x_coord - width_increment, 0, x_coord, gap_top);
      do Screen.drawRectangle(x_coord - width_increment, gap_bottom, x_coord, 255);
    }

    return;
  }

  /*
  Dispose of obstacle object.
  */
  method void dispose() {
     do Memory.deAlloc(this);

     return;
  }

}